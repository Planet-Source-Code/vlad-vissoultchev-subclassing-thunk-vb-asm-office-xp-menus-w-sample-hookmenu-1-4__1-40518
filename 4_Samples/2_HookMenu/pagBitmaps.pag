VERSION 5.00
Object = "{F9043C88-F6F2-101A-A3C9-08002B2F49FB}#1.2#0"; "Comdlg32.ocx"
Begin VB.PropertyPage pagBitmaps 
   Caption         =   "Menu Icons"
   ClientHeight    =   5136
   ClientLeft      =   0
   ClientTop       =   0
   ClientWidth     =   5472
   PaletteMode     =   0  'Halftone
   ScaleHeight     =   5136
   ScaleWidth      =   5472
   Begin VB.CommandButton cmdClear 
      Caption         =   "Clear"
      Height          =   288
      Left            =   4662
      Picture         =   "pagBitmaps.pgx":0000
      TabIndex        =   17
      Top             =   84
      Width           =   720
   End
   Begin MSComDlg.CommonDialog comDlg 
      Left            =   2856
      Top             =   1932
      _ExtentX        =   699
      _ExtentY        =   699
      _Version        =   393216
      CancelError     =   -1  'True
      Filter          =   "Icons And Graphics (*.ico;*.bmp;*.gif;*.jpg)|*.ico;*.bmp;*.gif;*.jpg|All files (*.*)|*.*"
      Flags           =   4
      FontBold        =   -1  'True
      FontItalic      =   -1  'True
      FontStrikeThru  =   -1  'True
      FontUnderLine   =   -1  'True
   End
   Begin VB.Frame fraControls 
      Caption         =   "Menu Icon"
      BeginProperty Font 
         Name            =   "MS Sans Serif"
         Size            =   7.8
         Charset         =   204
         Weight          =   700
         Underline       =   0   'False
         Italic          =   0   'False
         Strikethrough   =   0   'False
      EndProperty
      Height          =   1944
      Left            =   84
      TabIndex        =   8
      Top             =   3108
      Width           =   3120
      Begin VB.ComboBox cobMaskColor 
         Height          =   288
         Left            =   1092
         TabIndex        =   12
         Top             =   1512
         Width           =   1860
      End
      Begin VB.ComboBox cobMenus 
         Height          =   288
         Left            =   1092
         TabIndex        =   11
         Top             =   336
         Width           =   1860
      End
      Begin VB.PictureBox picBmp 
         Height          =   684
         Left            =   1092
         ScaleHeight     =   636
         ScaleWidth      =   1224
         TabIndex        =   10
         Top             =   756
         Width           =   1272
         Begin VB.Image imgBmp 
            Height          =   348
            Left            =   168
            MousePointer    =   15  'Size All
            Top             =   84
            Width           =   348
         End
      End
      Begin VB.CommandButton cmdOpen 
         Height          =   285
         Left            =   2436
         Picture         =   "pagBitmaps.pgx":014A
         Style           =   1  'Graphical
         TabIndex        =   9
         Top             =   756
         Width           =   300
      End
      Begin VB.Label Label4 
         Caption         =   "Menu Item:"
         Height          =   264
         Left            =   168
         TabIndex        =   15
         Top             =   336
         Width           =   936
      End
      Begin VB.Label Label3 
         Caption         =   "Mask Color:"
         Height          =   264
         Left            =   168
         TabIndex        =   14
         Top             =   1512
         Width           =   1020
      End
      Begin VB.Label Label2 
         Caption         =   "Bitmap:"
         Height          =   264
         Left            =   168
         TabIndex        =   13
         Top             =   756
         Width           =   1104
      End
   End
   Begin VB.Frame fraCommon 
      Caption         =   "Common"
      BeginProperty Font 
         Name            =   "MS Sans Serif"
         Size            =   7.8
         Charset         =   204
         Weight          =   700
         Underline       =   0   'False
         Italic          =   0   'False
         Strikethrough   =   0   'False
      EndProperty
      Height          =   1944
      Left            =   3360
      TabIndex        =   3
      Top             =   3108
      Width           =   2028
      Begin VB.CheckBox chkSystemFont 
         Caption         =   "Use system font"
         Height          =   348
         Left            =   168
         TabIndex        =   19
         Top             =   840
         Width           =   1692
      End
      Begin VB.ComboBox cobSize 
         Height          =   288
         Left            =   168
         TabIndex        =   6
         Top             =   1512
         Width           =   1356
      End
      Begin VB.CheckBox chkDisabled 
         Caption         =   "Select disabled menu items"
         Height          =   432
         Left            =   168
         TabIndex        =   4
         Top             =   336
         Width           =   1692
      End
      Begin VB.Label Label6 
         Caption         =   "px"
         Height          =   264
         Left            =   1680
         TabIndex        =   7
         Top             =   1512
         Width           =   264
      End
      Begin VB.Label Label5 
         Caption         =   "Bitmap size:"
         Height          =   264
         Left            =   168
         TabIndex        =   5
         Top             =   1260
         Width           =   1524
      End
   End
   Begin VB.CommandButton cmdAdd 
      Height          =   285
      Left            =   3996
      Picture         =   "pagBitmaps.pgx":0294
      Style           =   1  'Graphical
      TabIndex        =   2
      Top             =   84
      Width           =   300
   End
   Begin VB.ListBox lstMenus 
      Height          =   2640
      IntegralHeight  =   0   'False
      Left            =   84
      TabIndex        =   1
      Top             =   420
      Width           =   5304
   End
   Begin VB.CommandButton cmdRemove 
      Height          =   285
      Left            =   4332
      Picture         =   "pagBitmaps.pgx":03DE
      Style           =   1  'Graphical
      TabIndex        =   0
      Top             =   84
      Width           =   300
   End
   Begin VB.Label labNum 
      Height          =   264
      Left            =   1260
      TabIndex        =   18
      Top             =   84
      Width           =   936
   End
   Begin VB.Label labMenu 
      Caption         =   "Menu items:"
      Height          =   264
      Left            =   84
      TabIndex        =   16
      Top             =   84
      Width           =   1608
   End
End
Attribute VB_Name = "pagBitmaps"
Attribute VB_GlobalNameSpace = False
Attribute VB_Creatable = True
Attribute VB_PredeclaredId = False
Attribute VB_Exposed = True
'==============================================================================
' pagBitmaps.pag
'
'   Subclassing Thunk (SuperClass V2) Project Samples
'   Copyright (c) 2002 by Vlad Vissoultchev <wqweto@myrealbox.com>
'
'   Office XP menus control property page
'
' Modifications:
'
' 2002-10-28    WQW     Initial implementation
'
'==============================================================================
Option Explicit

'==============================================================================
' API
'==============================================================================

Private Const LB_SETTABSTOPS            As Long = &H192

Private Declare Function SendMessage Lib "user32" Alias "SendMessageA" (ByVal hwnd As Long, ByVal wMsg As Long, ByVal wParam As Long, lParam As Any) As Long

'==============================================================================
' Constants and member variables
'==============================================================================

Private m_oControl              As ctxHookMenu
Private m_cBmps                 As Collection
Private m_lLoaded               As Long
Private m_bInSet                As Boolean
Private m_bDrag                 As Boolean
Private m_sX                    As Single
Private m_sY                    As Single
Private m_bModified             As Boolean
Private m_bChanged              As Boolean

'==============================================================================
' Methods
'==============================================================================

Private Sub pvFillControls()
    Dim oCtl            As Object
    Dim vElem           As Variant
    Dim lIdx            As Long
    Dim lTop            As Long
    Dim lI              As Long
    Dim sText           As String
    Dim lItemData       As Long
    
    m_bInSet = True
    lIdx = lstMenus.ListIndex
    lTop = lstMenus.TopIndex
    lstMenus.Visible = False
    lstMenus.Clear
    For Each oCtl In m_oControl.frContainerMenus
        sText = vbTab & Replace(oCtl.Caption, vbTab, "\t") & vbTab & pvGetCtlName(oCtl)
        lItemData = 0
        For lI = 1 To m_cBmps.Count
            vElem = m_cBmps(lI)
            If vElem(2) = "#" & pvGetCtlName(oCtl) Then
                sText = "*" & sText
                lItemData = lI
            End If
        Next
        lstMenus.AddItem sText
        lstMenus.itemData(lstMenus.NewIndex) = lItemData
    Next
    lstMenus.Visible = True
    
    If lIdx < lstMenus.ListCount Then
        lstMenus.ListIndex = lIdx
    Else
        lstMenus.ListIndex = lstMenus.ListCount - 1
    End If
    lstMenus.TopIndex = lTop
    cobMenus.Clear
    For Each oCtl In m_oControl.frContainerMenus
        If Left(oCtl.Caption, 1) <> "-" Then
            cobMenus.AddItem pvGetCtlName(oCtl) & " (" & oCtl.Caption & ")"
        End If
    Next
'    If lstMenus.ListIndex >= 0 Then
'        cobMenus.Text = Split(lstMenus.Text, vbTab)(2)
'    End If
    chkDisabled.Value = IIf(m_oControl.SelectDisabled, vbChecked, vbUnchecked)
    chkSystemFont.Value = IIf(m_oControl.UseSystemFont, vbChecked, vbUnchecked)
    cobSize.Text = m_oControl.BitmapSize
    m_bInSet = False
    labNum = m_cBmps.Count & " icon(s)"
End Sub

Private Function pvGetCtlName(ByVal oCtl As Control) As String
    On Error Resume Next
    If oCtl.Index < 0 Then
        pvGetCtlName = oCtl.Name
    Else
        pvGetCtlName = oCtl.Name & ":" & oCtl.Index
    End If
End Function

Private Function pvUpload() As Boolean
    Dim vElem As Variant
    
    If m_lLoaded > 0 And Modified Then
        vElem = m_cBmps(m_lLoaded)
        m_cBmps.Remove m_lLoaded
        Set vElem(0) = imgBmp.Picture
        vElem(1) = Val("&H" & Mid(cobMaskColor.Text, 2))
        vElem(2) = "#" & Split(cobMenus.Text)(0)
        On Error Resume Next
        If m_lLoaded > m_cBmps.Count Then
            m_cBmps.Add vElem, vElem(2)
        Else
            m_cBmps.Add vElem, vElem(2), m_lLoaded
        End If
    End If
    m_oControl.SelectDisabled = (chkDisabled.Value = vbChecked)
    m_oControl.UseSystemFont = (chkSystemFont.Value = vbChecked)
    m_oControl.BitmapSize = Val(cobSize.Text)
    If Modified Then
        pvFillControls
        Modified = False
    End If
End Function

Private Sub pvCenterIcons()
    imgBmp.Move (picBmp.ScaleWidth - imgBmp.Width) \ 2, (picBmp.ScaleHeight - imgBmp.Height) \ 2
'    picBmp.Visible = False
'    picBmp.Visible = True
End Sub

Property Get Modified() As Boolean
    Modified = m_bModified
End Property

Property Let Modified(ByVal bValue As Boolean)
    m_bChanged = m_bChanged Or bValue
    m_bModified = bValue
    Changed = m_bChanged
End Property

'==============================================================================
' Control events
'==============================================================================

Private Sub chkDisabled_Click()
    If Not m_bInSet Then Modified = True
End Sub

Private Sub chkSystemFont_Click()
    If Not m_bInSet Then Modified = True
End Sub

Private Sub cmdAdd_Click()
    Dim vElem           As Variant
    Dim vSplit          As Variant
    
    If lstMenus.ListIndex >= 0 Then
        vSplit = Split(lstMenus.List(lstMenus.ListIndex), vbTab)
        If vSplit(0) = "" Then
            pvUpload
            ReDim vElem(0 To 2)
            Set vElem(0) = Nothing
            vElem(1) = &HFF00FF
            vElem(2) = "#" & vSplit(2)
            m_cBmps.Add vElem, vElem(2)
            pvFillControls
            lstMenus_Click
            Modified = True
        End If
    End If
End Sub

Private Sub cmdOpen_Click()
    On Error GoTo EH_Cancel
    comDlg.ShowOpen
    Set imgBmp = Nothing
    Set imgBmp.Picture = LoadPicture(comDlg.FileName)
    With New cMemDC
        .PaintPicture imgBmp.Picture
        cobMaskColor.Text = "#" & Hex(.GetPixel(0, .Height - 1))
    End With
    pvCenterIcons
    If Not m_bInSet Then Modified = True
EH_Cancel:
End Sub

Private Sub cmdRemove_Click()
    Dim vSplit          As Variant
    
    If lstMenus.ListIndex >= 0 Then
        vSplit = Split(lstMenus.List(lstMenus.ListIndex), vbTab)
        If vSplit(0) <> "" Then
            m_cBmps.Remove "#" & vSplit(2)
            m_lLoaded = 0
        End If
        pvFillControls
        m_lLoaded = 0
        lstMenus_Click
        Modified = True
        Changed = True
    End If
End Sub

Private Sub cobMaskColor_Change()
    If Not m_bInSet Then Modified = True
End Sub

Private Sub cobMaskColor_Click()
    If Not m_bInSet Then Modified = True
End Sub

Private Sub cobMenus_Change()
    If Not m_bInSet Then Modified = True
End Sub

Private Sub cobMenus_Click()
    If Not m_bInSet Then Modified = True
End Sub

Private Sub cobSize_Change()
    If Not m_bInSet Then Modified = True
End Sub

Private Sub cobSize_Click()
    If Not m_bInSet Then Modified = True
End Sub

Private Sub cmdClear_Click()
    Do While m_cBmps.Count > 0
        m_cBmps.Remove 1
    Loop
    m_lLoaded = 0
    pvFillControls
    lstMenus_Click
    Modified = True
End Sub

Private Sub lstMenus_Click()
    Dim vElem As Variant
        
    If m_bInSet Then
        Exit Sub
    End If
    pvUpload
    If lstMenus.ListIndex >= 0 Then
        m_lLoaded = lstMenus.itemData(lstMenus.ListIndex)
    Else
        m_lLoaded = 0
    End If
    If m_lLoaded > 0 Then
        m_bInSet = True
        fraControls.Visible = True
        vElem = m_cBmps(m_lLoaded)
        cobMenus.Text = Mid(vElem(2), 2)
        Set imgBmp.Picture = Nothing
        Set imgBmp.Picture = vElem(0)
        pvCenterIcons
        cobMaskColor.Text = "#" & Hex(vElem(1))
        m_bInSet = False
    Else
        fraControls.Visible = False
    End If
End Sub

Private Sub PropertyPage_Initialize()
    Dim aTabStop(0 To 1)    As Long
    
    cobMaskColor.AddItem "#FF00FF - Magenta"
    cobMaskColor.AddItem "#C0C0C0 - Grey"
    cobSize.AddItem "16"
    cobSize.AddItem "20"
    cobSize.AddItem "24"
    cobSize.AddItem "28"
    cobSize.AddItem "32"
    aTabStop(0) = 10
    aTabStop(1) = 120
    SendMessage lstMenus.hwnd, LB_SETTABSTOPS, 2, aTabStop(0)
End Sub

Private Sub PropertyPage_SelectionChanged()
    '--- this is the MOST bizarre way to handle it but VB freaks me out!!!
    '--- this event is raised upon EVERY mouse click on the property page
    '--- so when you are humbly selecting items in the listbox VB is alerting
    '--- that you actually changed the selected controls on the form which
    '--- is obviously not the case
    If Not m_oControl Is SelectedControls(0) Then
        m_bChanged = False
        m_lLoaded = 0
        Set m_oControl = SelectedControls(0)
        Set m_cBmps = m_oControl.frBmps
        pvFillControls
        lstMenus_Click
    Else
        '--- why is not VB remembering that values were changed
        '--- is completely beyond me!!
        Changed = m_bChanged
    End If
End Sub

Private Sub PropertyPage_ApplyChanges()
    pvUpload
    Set m_oControl.frBmps = m_cBmps
    Set m_oControl = Nothing
    '--- i never knew i had to manually clear Changed property!!!
    Changed = False
End Sub

Private Sub imgBmp_MouseDown(Button As Integer, Shift As Integer, X As Single, Y As Single)
    On Error Resume Next
    m_bDrag = True
    m_sX = X: m_sY = Y
End Sub

Private Sub imgBmp_MouseMove(Button As Integer, Shift As Integer, X As Single, Y As Single)
    On Error Resume Next
    If m_bDrag Then
        With imgBmp
            .Move .Left + (X - m_sX), .Top + (Y - m_sY)
        End With
    End If
End Sub

Private Sub imgBmp_MouseUp(Button As Integer, Shift As Integer, X As Single, Y As Single)
    On Error Resume Next
    imgBmp_MouseMove Button, Shift, X, Y
    With imgBmp
        If .Width < picBmp.ScaleWidth Then
            .Left = (picBmp.ScaleWidth - .Width) \ 2
        Else
            If .Left < picBmp.ScaleWidth - .Width Then
                .Left = picBmp.ScaleWidth - .Width
            End If
            If .Left > 0 Then
                .Left = 0
            End If
        End If
        If .Height < picBmp.ScaleHeight Then
            .Top = (picBmp.ScaleHeight - .Height) \ 2
        Else
            If .Top < picBmp.ScaleHeight - .Height Then
                .Top = picBmp.ScaleHeight - .Height
            End If
            If .Top > 0 Then
                .Top = 0
            End If
        End If
    End With
    m_bDrag = False
End Sub

